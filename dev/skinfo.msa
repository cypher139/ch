<!
	strict: on;
	name: skinfo.msa;
	author: cypher139;
	created: 2016-03-14;
	description: Provides info about various player interactions with WorldEdit WorldGuard plugins.;
	requiredExtensions: Core, SKCompat;
>

everyone:/regionat [$] = >>>
@name = 'regionat'
@you = player()
@player = ''
@args = parse_args($)
#Command help file
proc _ch_help() { 
	msg(color(6).'Find what WorldGuard regions a player is in:')
	msg(color(10).'Command usage: <required> [optional]')
	msg(color(6).'Actions:')
	msg(color(7).'/regionat [player]'.color(4).' - '.color('f').'Returns what WG region(s) the specified player is currently in. Player defaults to you if not specified.')
}

foreach(@arg in @args) {
	if(@arg == 'help') { _ch_help() die() }
	if(array_contains_ic(all_players(), @arg)) {
		@player = @arg
	}
}
if(@player == '') { @player = player()}
if(@player == '~console') {
	die(color(12).'['.color(7).@name.color(12).'] '.color(7).'The console cannot be in a WorldGuard region!')
}

if(array_contains_ic(all_players(), @player)) {
	@here = ploc(@player)
	msg(color(7).'['.color(6).@name.color(7).'] '.color(6).'WG Regions at: '.color(7).@player.' ('.@here.')')
	msg(color(7).sk_regions_at(@here))
} else {
	die(color(7).'['.color(6).@name.color(7).'] '.color(9).'You need to specify a player!')
}
<<<



everyone:/cpos [$] = >>>
@name = 'CurrentPos'
@you = player()
@player = ''
@args = parse_args($)
#Command help file
proc _ch_help() {
	msg(color(6).'Find what WorldEdit selection a player has made!')
	msg(color(10).'Command usage: <required> [optional]')
	msg(color(7).'/cpos [player]'.color(4).' - '.color('f').'Returns what WE selection the specified player has made. Defaults to you if a player isn\'t specified.')
}
foreach(@arg in @args) {
	if(@arg == 'help') { _ch_help() die() }
	if(array_contains_ic(all_players(), @arg)) {
		@player = @arg
	}
}
if(@player == '') { @player = player()}
if(@player == '~console') {
	die(color('c').'['.color(7).@name.color('c').'] '.color(7).'The console cannot use WorldEdit!')
}

@youmsg = @youmsg = color(3).@player.'\'s'
if(@you == @player) { @youmsg = color(6).'Your' }
msg(color(7).'['.color('gold').@name.color(7).'] '.@youmsg.color(6).' Worldedit selection: ')
msg(color(13).'Position #1: '.sk_pos1(@player)['x'].','.sk_pos1(@player)['y'].','.sk_pos1(@player)['z'])
msg(color(13).'Position #2: '.sk_pos2(@player)['x'].','.sk_pos2(@player)['y'].','.sk_pos2(@player)['z'])
<<<