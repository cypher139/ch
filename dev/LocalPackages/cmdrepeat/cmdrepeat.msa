#version 1.2-tmc
everyone:/repeat [$cmd] = >>>
@player = _getplayerID(player())

if(is_numeric($cmd) && $cmd > 0) {
	if($cmd - 1 > 9) { @cmd = 9 } else { @cmd = $cmd - 1 }
} else {
	@cmd = to_lower($cmd)
}

switch(@cmd) {
	case 'allcmds':
		if(has_permission(@player['name'], 'commandhelper.alias.admin')) { msg(import('cmdrepeat.all.commands')) }
		die()
	case 'printrepeatcommand':
	case 'printrepeatcmd':
	case 'printrepeat':
	case 'showrepeat':
	case 'hiderepeat':
	case 'showr':
	case 'hider':
	case 'pr':
		@setting = get_value('cmdrepeat.player.'.@player['id'].'.command.show')
		if(@setting == true) {
			#disable
			store_value('cmdrepeat.player.'.@player['id'].'.command.show', false)
			export('cmdrepeat.player.'.@player['id'].'.command.show', false)
			die(color(7).'['.color(6).'Command'.color(7).'] '.color('r').' The command re-run will be '.color('c').'hidden'.color('r').' and not shown to you in the chat.')
		} else {
			#enable
			store_value('cmdrepeat.player.'.@player['id'].'.command.show', true)
			export('cmdrepeat.player.'.@player['id'].'.command.show', true)
			die(color(7).'['.color(6).'Command'.color(7).'] '.color('r').' The command re-run will be '.color('a').'shown'.color('r').' only to you in the chat.')
		}
	case 'printcommand':
	case 'printcmd':
	case 'print':
	case 'show':
	case 'hide':
	case 'printall':
	case 'p':
		@setting = get_value('cmdrepeat.player.'.@player['id'].'.command.show.all')
		if(@setting == true) {
			#disable
			store_value('cmdrepeat.player.'.@player['id'].'.command.show.all', false)
			export('cmdrepeat.player.'.@player['id'].'.command.show.all', false)
			die(color(7).'['.color(6).'Command'.color(7).'] '.color('r').'Any command you run will be '.color('c').'hidden'.color('r').' and not printed back to you in the chat.')
		} else {
			#enable
			store_value('cmdrepeat.player.'.@player['id'].'.command.show.all', true)
			export('cmdrepeat.player.'.@player['id'].'.command.show.all', true)
			die(color(7).'['.color(6).'Command'.color(7).'] '.color('r').'Any command you run will be '.color('a').'shown'.color('r').', printed back only to you in the chat.')
		}
	case 'consolecmd':
	case 'console':
	case 'consolecommand':
	case 'cc':
		@setting = get_value('cmdrepeat.console.command.show')
		if(has_permission(@player['name'], 'commandhelper.alias.admin')) {
			if(@setting == true) {
				#disable
				store_value('cmdrepeat.console.command.show', false)
				export('cmdrepeat.console.command.show', false)
				die(color(7).'['.color(6).'Command'.color(7).'] '.color('r').'All commands will '.color('c').'NOT broadcast'.color('r').' to the console. This does not affect any command broadcast messages other plugins or the vanilla server may provide.')
			} else {
				#enable
				store_value('cmdrepeat.console.command.show', true)
				export('cmdrepeat.console.command.show', true)
				die(color(7).'['.color(6).'Command'.color(7).'] '.color('r').'All commands will '.color('a').'be broadcast'.color('r').' to the console. This does not affect any command broadcast messages other plugins or the vanilla server may provide.')
			}
		} else {
			die(color('c').'['.color(7).'Command'.color('c').'] '.color(3).'You do not have permissions to use this command option.')
		}
/*
	case 'moo':
		die('\n'.moo(color('c').'['.color(7).'Command'.color('c').'] '.color(3).'Unexpected action.'.color('r')))
*/
	case 'recent':
	case 'r':
	case 'last':
	case 'list':
		@lastcmd = import('cmdrepeat.player.'.@player['id'].'.last.command')
		if(@lastcmd == '' || @lastcmd == null) { die(color('c').'['.color(7).'Command'.color('c').'] '.color('c').'Any Recent Commands were not found.') }
		msg(color(7).'['.color(6).'Command'.color(7).'] '.color(3).'Your '.array_size(@lastcmd).' most recent Commands:')
		foreach(@k: @v in @lastcmd) {
			msg(@k + 1.' > '.@v)
		}	
		die()		
	case '':
	case null:
	case 0:
		@cmdid = 0
	case 1..9:
		@cmdid = @cmd
	default:
		die(color('c').'['.color(7).'Command'.color('c').'] '.color(3).'Invalid action specified.')
}
	if(@player['name'] == '~console', die(color('c').'['.color(7).'Command'.color('c').'] '.color(3).'Console cannot repeat commands. Use the Up arrow to repeat a command.'))
	@lastcmd = import('cmdrepeat.player.'.@player['id'].'.last.command')
	@showcmd = get_value('cmdrepeat.player.'.@player['id'].'.command.show')
	if(@lastcmd == '' || @lastcmd == null) { die(color('c').'['.color(7).'Command'.color('c').'] '.color('c').'Any Recent Commands were not found.'.color('r').' Did you just login? ('.(@cmdid + 1).')') }
	if(!array_index_exists(@lastcmd, @cmdid)) { die(color('c').'['.color(7).'Command'.color('c').'] '.color('c').'Recent Command #'.(@cmdid + 1).' not found.') }
	#List cmd used, with CH aliases showing as a different color.
	if(is_alias(reg_split(' ', @lastcmd[@cmdid])[0])) { @cmdcolor = 3 } else { @cmdcolor = 6 }
	#msg(@lastcmd)
	msg(color(3).'Re-ran your last command. (#: '.(@cmdid + 1).')')
	if(@showcmd == true) {
		if(!has_permission(@player['name'], 'commandhelper.alias.see.is.alias')) { @cmdcolor = 7 }
		msg('> '.color(@cmdcolor).@lastcmd[@cmdid])
	}
	run_cmd(@lastcmd[@cmdid])
<<<

'/.' [$cmd] = >>>
call_alias(/repeat $cmd)
<<<

'/\'' [$cmd] = >>>
call_alias(/repeat $cmd)
<<<

'//.' [$cmd] = >>>
call_alias(/repeat 2)
<<<