#version 1.1-tmc
everyone:/repeat [$action] = >>>
@player = _cmd_getplayerID(player())

switch(to_lower($action)) {
	case 'allcmds':
		msg(import('cmdrepeat.all.commands'))
	case 'printrepeatcommand':
	case 'printrepeatcmd':
	case 'printrepeat':
	case 'showrepeat':
	case 'hiderepeat':
	case 'showr':
	case 'hider':
	case 'pr':
		assign(@setting, get_value('cmdrepeat.player.'.@player['id'].'.command.show'))
		if(@setting == true) {
			#disable
			store_value('cmdrepeat.player.'.@player['id'].'.command.show', false)
			export('cmdrepeat.player.'.@player['id'].'.command.show', false)
			die(color(7).'['.color(6).'Command'.color(7).'] '.color('r').' The command re-run will be '.color('c').'hidden'.color('r').' and not shown to you in the chat.')
		} else {
			#enable
			store_value('cmdrepeat.player.'.@player['id'].'.command.show', true)
			export('cmdrepeat.player.'.@player['id'].'.command.show', true)
			die(color(7).'['.color(6).'Command'.color(7).'] '.color('r').' The command re-run will be '.color('a').'shown'.color('r').' only to you in the chat.')
		}
	case 'printcommand':
	case 'printcmd':
	case 'print':
	case 'show':
	case 'hide':
	case 'printall':
	case 'p':
		assign(@setting, get_value('cmdrepeat.player.'.@player['id'].'.command.show.all'))
		if(@setting == true) {
			#disable
			store_value('cmdrepeat.player.'.@player['id'].'.command.show.all', false)
			export('cmdrepeat.player.'.@player['id'].'.command.show.all', false)
			die(color(7).'['.color(6).'Command'.color(7).'] '.color('r').'Any command you run will be '.color('c').'hidden'.color('r').' and not printed back to you in the chat.')
		} else {
			#enable
			store_value('cmdrepeat.player.'.@player['id'].'.command.show.all', true)
			export('cmdrepeat.player.'.@player['id'].'.command.show.all', true)
			die(color(7).'['.color(6).'Command'.color(7).'] '.color('r').'Any command you run will be '.color('a').'shown'.color('r').', printed back only to you in the chat.')
		}
	case 'consolecmd':
	case 'console':
	case 'consolecommand':
	case 'cc':
		assign(@setting, get_value('cmdrepeat.console.command.show'))
		if(has_permission(@player['name'], 'commandhelper.alias.admin')) {
			if(@setting == true) {
				#disable
				store_value('cmdrepeat.console.command.show', false)
				export('cmdrepeat.console.command.show', false)
				die(color(7).'['.color(6).'Command'.color(7).'] '.color('r').'All commands will '.color('c').'NOT broadcast'.color('r').' to the console. This does not affect any command broadcast messages other plugins or the vanilla server may provide.')
			} else {
				#enable
				store_value('cmdrepeat.console.command.show', true)
				export('cmdrepeat.console.command.show', true)
				die(color(7).'['.color(6).'Command'.color(7).'] '.color('r').'All commands will '.color('a').'be broadcast'.color('r').' to the console. This does not affect any command broadcast messages other plugins or the vanilla server may provide.')
			}
		} else {
			die(color('c').'['.color(7).'Command'.color('c').'] '.color(3).'You do not have permissions to use this command option.')
		}
	case 'moo':
		die('\n'.moo(color('c').'['.color(7).'Command'.color('c').'] '.color(3).'Unexpected action.'.color('r')))
	case 'recent':
	case 'r':
	case 'last':
	case 'list':
		msg(color(7).'['.color(6).'Command'.color(7).'] '.color(3).'Your 3 Recent Commands:')
		for(assign(@i, 1), @i < 4, @i++) {
			assign(@lastcmd, import('cmdrepeat.player.'.@player['id'].'.last.command.'.@i))
			if(@lastcmd) {			
				msg('> '.@lastcmd)
			}	
		}		
	case '':
	case null:
	case '1':
	case '2':
	case '3':
		if($action == '' || $action == null) {
			assign(@cmdid, 1)
		} else {
			assign(@cmdid, $action)
		}
		if(@player['name'] == '~console', die(color('c').'['.color(7).'Command'.color('c').'] '.color(3).'Console cannot repeat commands. Use the Up arrow to repeat a command.'))
		assign(@lastcmd, import('cmdrepeat.player.'.@player['id'].'.last.command.'.@cmdid))
		assign(@showcmd, get_value('cmdrepeat.player.'.@player['id'].'.command.show'))
			if(@lastcmd == '' || @lastcmd == null,
				die(color('c').'['.color(7).'Command'.color('c').'] '.color('c').'A Recent command was not found.'.color('r').' Did you just login? ('.@cmdid.')')
			)
		#List cmd used, with CH aliases showing as a different color.
		if(is_alias(reg_split(' ', @lastcmd)[0]), assign(@cmdcolor, 3), assign(@cmdcolor, 6))
		#msg(@lastcmd)
		msg(color(3).'Re-ran your last command. (#: '.@cmdid.')')
		if(@showcmd == true,
			if(has_permission(@player['name'], 'commandhelper.alias.see.is.alias'), null,
				#CH builds < 2964: Repeat cmd prints back the command to run in color code #7
				@cmdcolor = 7
			)
			msg('> '.color(@cmdcolor).@lastcmd)
		)
		run_cmd(@lastcmd)
	default:
		msg(color('c').'['.color(7).'Command'.color('c').'] '.color(3).'Invalid action specified.')
}
<<<

'/.' [$action] = >>>
call_alias(/repeat $action)
<<<

'/\'' [$action] = >>>
call_alias(/repeat $action)
<<<

'//.' [$action] = >>>
call_alias(/repeat 2)
<<<