######################################################
#
#   ReLoad: Fill blocks that have inventories in one click!
#   Administration: Manage settings for Reloader.
#   version 1.2-tmc
#
######################################################
admin:/reloaderadmin [$action] [$option1] [$option2] [$option3] [$] = >>>
#Include required backend functions
include('modules.library/Core.ms')
@name = string(import('dispenserreloader.name'))
@serverdefaults = _reloader_get_server_defaults()
 
@action = to_lower($action)
@option1 = $option1
@option2 = $option2
@option3 = $option3
@player = _reloader_getplayerID(player())
@dev = import('ch.player.'.@player['id'].'.dev.scripts.reload')
 
@help = closure(@ex) {
	@return = array(
	color(6).'ReLoad admin help:',
	color('a').'Command usage: <required> [optional]',
	color('a').'/reloaderadmin <action> [option1] [option2] [option3]',
	color(6).'Actions:',
	color(7).'/reloaderadmin stats'.color(4).' - '.color('f').'Shows all time stats of '.@name.', such as total blocks filled, total items used, and total fees charged.',
	color(7).'/reloaderadmin stats player <player name>'.color(4).' - '.color('f').'Shows the specified player\'s '.@name.' stats.',
	color(7).'/reloaderadmin version'.color(4).' - '.color('f').'Info of script and installed extensions version numbers.',
	color(7).'/reloaderadmin default view'.color(4).' - '.color('f').'Allows you to view the current default settings.',
	color(7).'/reloaderadmin default item <type/qty/fuel> <value>'.color(4).' - '.color('f').'Allows you to set the server\'s default item type and quantity, or default item to use as fuel in Fuel mode. Use \'clear\' as the value to reset to script defaults.'
	)
	return(array_implode(@return, '\n'))
}
 
switch(@action){

case 'throw':
	# throw('ms.lang.CastException', 'testing throw')
include('modules.library/Throw.ms')
	_reloader_throw()
case 'help':
case '-help':
case '?':
case '/?':
	die(@help())

case 'version':
case 'build':
case 'info':
	#Due to different extension names checking for functionality instead.
	msg(color(7).'['.color(6).@name.color(7).'] '.color(3).@name.' Version: '.import('dispenserreloader.version'))
	msg(color(7).'['.color(6).@name.color(7).'] '.color(1).'Required Extensions:')
	#Area mode: uses WorldEdit selection data.
	if(function_exists('sk_pos1')) {
		foreach(@extname: @extinfo in extension_info()) {
			if(array_contains(@extinfo['functions'], 'sk_pos1')) {
				@ext = @extname
				@extver = @extinfo['version']
				break()
			}
		}
		msg(color(7).'['.color(6).@name.color(7).'] '.color(9).'Area Selection: '.color('a').@ext.color(7).' (Version: '.@extver.')')
	} else {
		msg(color('c').'['.color(7).@name.color('c').'] '.color('c').@name.' is missing access to WorldEdit selections!'.color(7).' Area mode is disabled without access to selection data.')
		msg(color(3).'Download the SKCompat extension from the downloads link below and save it to <server root>/plugins/CommandHelper/extensions.')
		msg('\n'.moo(color(2).'https://letsbuild.net/jenkins/job/SKCompat/'.color('r')))
	}
	#Economy - Charges fees for usage of script
	if(function_exists('acc_add')) {
		foreach(@extname: @extinfo in extension_info()) {
			if(array_contains(@extinfo['functions'], 'acc_add')) {
				@ext = @extname
				@extver = @extinfo['version']
				break()
			}
		}
		msg(color(7).'['.color(6).@name.color(7).'] '.color(9).'Economy: '.color('a').@ext.color(7).' (Version: '.@extver.')')
	} else {
		msg(color('c').'['.color(7).@name.color('c').'] '.color('c').@name.' is missing access to economy functions and account data!'.color(7).'All script functions will be unable to utilize your server economy!!')
		msg(color(3).'Download the CHVault extension from the downloads link below and save it to <server root>/plugins/CommandHelper/extensions.')
		msg('\n'.moo(color(2).'https://letsbuild.net/jenkins/job/CHVault/'.color('r')))
	}

case 'stats':
case 'statistics':
case '-stats':
case 'stat':
case 'st':
	@viewplayer = false
	# Stats per player
	if(@option1 != '') {
		if(array_contains_ic(get_offline_players(), @option1)) {
			@viewplayer = @option1
		} else {
			die(color('c').'['.color(7).@name.color('c').'] '.color(7).'Please specify a valid player name!')
		}
	}
	switch(@viewplayer) {
	case false:
		@idprefix = @name 
		@startmsg = 'Global stats:'
		@lr = get_value(@idprefix.'.stats.last.runs')
		if(!is_array(@lr)) { @lr = array('action': 'Run Not Found', 'blocks': 0, 'filledqty': 0, 'item': 'DIRT') } else { @lr = @lr[0] }
	default:
		@player = _reloader_getplayerID(@option1)
		@idprefix = @name.'.player.'.@player['id']
		@startmsg = 'stats for: '.color(9).@player['name'] 
		@lr = 0
	}
	msg(color(7).'['.color(6).@name.color(7).'] '.color(7).@name.' '.@startmsg) 
	msg(color(9).' -------')
	
	@stats = get_value(@idprefix.'.stats')
	if(!is_array(@stats)) { @stats = array('filled_blocks': 0, 'items': array(), 'runs_area': 0, 'runs_tool': 0) }
 
	@atr = @stats['runs_area']
	@ttr = @stats['runs_tool']
	# Stop with 0/null stuff if the player or server has not used the script.
	if(@atr == 0 && @ttr == 0) { 
		if(@viewplayer == false) { @msgp = 'server' } else { @msgp = 'player' }		
		die(color('c').'['.color(7).@name.color('c').'] '.color('c').'This '.@msgp.' has not used '.@name.'!') 
	}
	msg(color(7).'All time '.@name.' runs:')
	msg(color(6).'/reload area: '.color('b')._reloader_number_commaize(@atr).' runs')
	msg(color(6).'/reload (tool): '.color('b')._reloader_number_commaize(@ttr).' runs')
	msg(color(6).'Total '.@name.' runs: '.color('b')._reloader_number_commaize(@atr + @ttr).' runs')
	
	msg(color(9).' -------')
 
	msg(color(7).'Total blocks filled: '.color('b')._reloader_number_commaize(@stats['filled_blocks']))
	
	msg(color(9).' -------')
	
	if(@lr == 0) { @lr = @stats['last_run'] }
 	if(is_array(@lr)) {
		if(array_index_exists(@lr, 'action')) {	
			if(@viewplayer == false) { @lrpm = ', by '.@lr['player'].'.' } else { @lrpm = '.' }
			msg(color(7).'Last run: '.color(6).@lr['action'].@lrpm)
			if(@lr['blocks'] == 1) { assign(@p, 'block') assign(@p2, 'was') } else { assign(@p, 'blocks') assign(@p2, 'were') }
			msg(color('a')._reloader_number_commaize(@lr['blocks']).' '.@p.color(7).' '.@p2.' filled, using '.color('b')._reloader_number_commaize(@lr['filledqty']).' '.color(3).to_lower(@lr['item']).'.')
		}
	}
	msg(color(9).' -------')
 
	if(is_array(@stats['items'])) { 
		msg(color(9).'Total items used to fill blocks:')	
	} else {
		@stats['items'] = array()
	}
	@tiu = 0
	foreach(@itemname: @itemqty in @stats['items']) {
		@tiu = @tiu + @itemqty
		msg(color(3)._reloader_item_name(@itemname)['display'].': '.color(7)._reloader_number_commaize(@itemqty))
	} else {     
		die(color(7).'['.color(6).@name.color(7).'] '.color(7).'No blocks have been filled?! Try using /reload to refill your block inventories!')
	}
	msg(color(9).'Total items used: '._reloader_number_commaize(@tiu))
	
	die(color(9).'['.color(7).@name.color(9).'] '.color(9).' -----')
case 'default':
case 'setdefault':
case '-default':
case 'd':
case 'set':
	switch(to_lower(@option1)) {        
	case 'type':
	case 't':
	case 'name':
	case 'n':
	case 'fuel':
	case 'f':
	case 'item':
	case 'i':
		@olditem = @serverdefaults['item']
		#fuel Check
		switch(to_lower(@option1)) { 
		case 'f':
		case 'fuel':
			@fuel = true
		default:
			@fuel = false
		}
		if(@fuel == true) {
			@olditem = @serverdefaults['fuel']
		}
		#Clear check
		switch(to_lower(@option2)) {
		case '-':
		case 'remove':
		case 'default':
		case 'clear':
		case '0':
		case 'air':
			@serverdefaults = _reloader_get_server_defaults('clearitem')
			@item = @serverdefaults['item']
			if(@fuel == true) {
				@item = @serverdefaults['fuel']
			}
		default:
			@item = @option2
			if(@option2 == '' || @option2 == null) {
				die(color('c').'['.color(6).@name.color('c').'] '.color(7).'You didn\'t specify the item to use!')
			}
		}
		# Check: Did you specify a item name or ID?
		# We need to translate the item given into the proper Item Name.
		#Perform Name / ID checks.
		@itemids = _reloader_item_name(@item)
		@item = @itemids['name']
		@itemname = @itemids['name']

		# Check: Is this air?
		if(@item == '0' || string_contains_ic(@item, 'air')) {
			_reloader_msgAdmins(color('c').'['.color(6).@name.color('c').'] '.color('c').@player['name'].' tried to set the default item to air in '.@name.'! ')
			die(color('c').'['.color(6).@name.color('c').'] '.color(7).'You cannot use air!')
		}

		if(@item == null || @item == '') {
			_reloader_msgAdmins(color('c').'['.color(6).@name.color('c').'] '.color('c').@player['name'].' tried to set the default item to a non-vanilla item in '.@name.'! '.color(7).'Only vanilla items are supported!', ' Note: If you are running a modded server, you will need to notify CommandHelper of the data names for blocks/items that mods add. Ask on CommandHelper\'s IRC channel for help in doing this.', true)
			die(color('c').'['.color(6).@name.color('c').'] '.color(7).'That item isn\'t supported!')
		}
		if(@fuel == true) {
			store_value(@name.'.default.item.type.fuel', @item)
			export(@name.'.default.item.type.fuel', @item)
		} else {
			store_value(@name.'.default.item.type', @item)
			export(@name.'.default.item.type', @item)
		}
		msg(color('a').'['.color(6).@name.color('a').'] '.color(3).'The default item type was: '.color(7).@olditem.color(3).', and is now: '.color('a').@item.' ('.@itemids['id'].')')
	case 'qty':
	case 'quantity':
	case 'q':
		@oldqty = @serverdefaults['qty']
		switch(to_lower(@option2)) {
		case '-':
		case 'remove':
		case 'default':
		case 'clear':
			@serverdefaults = _reloader_get_server_defaults('clearqty')
			@option2 = @serverdefaults['qty']
		case '0':
			die(color('c').'['.color(6).@name.color('c').'] '.color(7).'You cannot use a quantity of 0!')
		case '':
		case null:
				die(color('c').'['.color(6).@name.color('c').'] '.color(7).'You didn\'t specify the item quantity!')
		}
		# Check: Did you specify the quantity in a number?
		if(is_integral(@option2)) {
			if(@dev == true, msg(color(7).'[DEV] is_integral: '.color('f').'yes'))
		} else {
			if(@dev == true, msg(color(7).'[DEV] is_integral: '.color('f').'no'))
			die(color('c').'['.color(6).@name.color('c').'] '.color(7).'You need to specify the quantity in a number.')	
		}
		store_value(@name.'.default.item.qty', @option2)
		export(@name.'.default.item.qty', @option2)
		msg(color('a').'['.color(6).@name.color('a').'] '.color(3).'The default item quantity was: '.color(7).@oldqty.color(3).', and is now: '.color('a').@option2)
	default:
		die(color('c').'['.color(6).@name.color('c').'] '.color(7).'You need to specify a item name or quantity (qty) to set as the default.')        
	}

case 'view':
case 'settings':
case 'current':
	msg(color(7).'['.color(6).@name.color(7).'] '.color('b').@name.' defaults:')
	msg(color(7).'['.color(6).@name.color(7).'] '.color(3).'Item type: '.color(7)._reloader_item_name(@serverdefaults['item'])['name'])
	msg(color(7).'['.color(6).@name.color(7).'] '.color(3).'Item quantity: '.color(7).@serverdefaults['qty'])
	msg(color(7).'['.color(6).@name.color(7).'] '.color(3).'Fuel: Item type: '.color(7)._reloader_item_name(@serverdefaults['fuel'])['name'])
	die()
default:
	msg(color('c').'['.color(6).@name.color('c').'] '.color(7).'You didn\'t specify what to do! Let\'s view the help file.')
	die(@help())
}	
<<<