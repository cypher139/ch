#version 1.0-tmc
admin:/discordtransferroles [$from] [$to] [$] = >>>
@name = 'Discord'
@roles = get_value('server.Discord.members.roles')
@args = parse_args($)
if(!array_index_exists(@args, 0)) { @args = array('none') }
if($from == '' || $from == null) {
	die(color('c').'Specify the Discord user ID to copy from!')
}
if($to == '' || $to == null) {
	die(color('c').'Specify the Discord user ID to copy to!')
}
# From User check
try {
	@fromroles = discord_member_get_roles($from)
} catch(Exception @ex) {
	if(is_array(@roles)) {
		if(array_index_exists(@roles, $from)) {
			@fromroles = @roles[$from]
			msg(color('e').'From ID: Not Found on Server, using internal storage.')
		} else {
			die(color('c').'From ID was Not Found on Server or internal storage!')
		}
	} else { die(color('c').'From ID was Not Found on Server or internal storage!') }
}
# To User check
try {
	@toroles = discord_member_get_roles($to)
} catch(Exception @ex) {
	die(color('c').'To ID Not Found on Server: '.@ex['message'])
}
if(array_contains(@args, 'undo')) {
#does not add from roles.
	@toroles = get_value('Chat.debug.Discord.User.'.$to.'.old.roles')
	if(!is_array(@toroles)) { die(color('c').'That user has nothing to undo!') }
} else {
# adds from roles, not sets to!
	store_value('Chat.debug.Discord.User.'.$to.'.old.roles', @toroles)
	@toroles = array_merge(@toroles, @fromroles)
}
	# console(@toroles, false)
# for normal array only:
# array_push_all(@toroles, array_normalize(@fromroles))
# @toroles = array_unique(@toroles)
	# console(@fromroles)
	
discord_member_set_roles($to, @toroles)
msg(color('a').'Discord User '.color(3).$to.color('a').' had their Roles set to:\n'.color('r').array_keys(@toroles))
<<<