#version 1.0-tmc
#add setup for welcome message/dm, or file.
# %USER% = etc
admin:/discorddm123 [$id] [$] = >>>
@name = 'Discord'
@DMsrec = get_value('server.Discord.DMs.received')
if(!is_array(@DMsrec)) { @DMsrec = array(array('userid': 'none', 'message': 'initialized DM Array')) }
switch($id) {
	case '':
	case null:
		die(color('c').'You must specify a user ID to send a DM to!')
	case 'read':
	case 'view':
	case 'open':
		if(!has_permission(player(), 'commandhelper.alias.Discord.DM.read')) { die(color('c').'You do not have permission to read DMs!?') }
		
		if($ == '') { die(color('c').'You must specify a message to read!') }
		@msgid = parse_args($)[0]
		if(!is_numeric(@msgid)) { die(color('c').'You must specify the recent history number of the DM to read.') }
		if(@msgid - 1 > 19) { @msgid = 19 } else { @msgid = @msgid - 1 }
	
		if(array_index_exists(@DMsrec, @msgid, 'message')) {
			@sendmsg = @DMsrec[@msgid]['message']
			if(array_index_exists(@DMsrec, @msgid, 'attachments', 0, 'filename')) {
				if(@sendmsg == '') {			
					@sendmsg = '<Sent a file> '
				} else {
					@sendmsg = @sendmsg.'\n <Also attached file:> '
				}
				foreach(@num: @attach in @DMsrec[@msgid]['attachments']) {
					if(@num == 0) { @filenum = 'File: ' } else { @filenum = '  File #'.(@num + 1).': ' }
					@sendmsg = @sendmsg.@filenum.@attach['filename']
				}
			}
			msg(color(7).'Recent DM #'.(@msgid + 1))
			msg(color(9).'DM from ['.color(7).@DMsrec[@msgid]['username'].color(9).'] sent at: '.simple_date('MM/dd/yyyy HH:mm:ss', @DMsrec[@msgid]['time']).'. Message:\n'.color(7).@sendmsg)

			if(array_index_exists(@DMsrec, @msgid, 'attachments', 0, 'filename')) {
				foreach(@num: @attach in @DMsrec[@msgid]['attachments']) {
					msg(color(8).'URL #'.(@num + 1).': '.@attach['url'])
				#file and url version	msg(color(8).'File #'.(@num + 1).': '.@attach['filename'].'   URL :'.@attach['url'])
				}
			}
		} else {
			die(color('e').'['.color(6).@name.color('e').'] '.color(7).'Sorry, there is no message at Recent DM #'.@msgid.'!')
		}
		die()
	case 'r':
	case 'reply':
	case 're':
		@id = @DMsrec[0]['userid']
	default:
		@id = $id
}
#if(!is_numeric($id)) {die(color('c').'You must specify a user ID numerically! Right click the user profile and select Copy ID.') } Not needed, username without tag accepted
msg(color(4).'With Great Power comes Great Responsibility.'.color(3).' Please ensure the messages you send are family-friendly and do not harrass others.')

if($ == '') { die(color('c').'You must specify a message to send!') }
@sendmsg = strip_colors($)

try(
	#Send Message
	discord_private_message(@id, '`['.player().']` '.@sendmsg)
	msg(color(6).'Message Sent')
	#Save recent DMs for future reference if needed
	@DMs = get_value('server.Discord.DMs.sent')
	if(!is_array(@DMs)) { @DMs = array(array('message': 'initialized DM Array')) }
	array_insert(@DMs, array('sender': player(), 'message': @sendmsg, 'time': time()), 0)
	if(array_index_exists(@DMs, 20)) { array_remove(@DMs, 20) }
	store_value('server.Discord.DMs.sent', @DMs)
,@fail,
	msg(color('c').'Message Not Sent')
	msg(color('7').@fail['message'])
)
<<<