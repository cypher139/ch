#Rainbow chat!
#version 1.1-tmc
#todo? make actual rainbow colors, expanded to fit length(@msg) like ^0(...)

*:/rainbow [$] = >>>
@name = 'Rainbow'
@chatname = import('chatmodifier.name', 'Chat')
assign(@args, parse_args($))
assign(@player, _chat_getplayerID(player()))
@prefix = color(7).'['.color(6).@name.color(7).'] '

if(!has_permission(@player['name'], 'commandhelper.alias.chat.use.rainbow')) { die(color('c').'['.color(7).'Rainbow'.color('c').'] '.color('c').'You do not have permission to use Rainbow chat!') }
if($ == '') { die(color('c').'['.color(7).@name.color('c').'] '.'You didn\'t say anything!!') }
@msg = strip_colors($)

#specify action
@action = ''
#toggle action
if(reg_count('^[-\\/][Tt].*', @args[0]) > 0) {
	if(array_index_exists(@args, 1)) {
		@settype = 'normal'
		if(reg_count('^[Dd]', @args[1]) > 0) { @settype = 'doubles' }
		if(reg_count('^[Nn]', @args[1]) > 0) { @settype = 'normal' }
		if(reg_count('^[Aa]', @args[1]) > 0) { @settype = 'all' }
		if(reg_count('^[Ww]', @args[1]) > 0) { @settype = 'words' }
		if(reg_count('^[Pp]', @args[1]) > 0) { @settype = 'ps2' }

		store_value(@chatname.'.player.'.@player['id'].'.chat.rainbow.toggle', true)
		store_value(@chatname.'.player.'.@player['id'].'.chat.rainbow.type', @settype)
		export(@chatname.'.player.'.@player['id'].'.chat.rainbow.toggle', true)
		export(@chatname.'.player.'.@player['id'].'.chat.rainbow.type', @settype)
		die(@prefix.color('d')._rainbow('Automatic rainbow chat is now enabled!', @settype).color('a').' Your chat will be like the colors of the rainbow now!')

	} else {
		if(get_value(@chatname.'.player.'.@player['id'].'.chat.rainbow.toggle') == true) {
			store_value(@chatname.'.player.'.@player['id'].'.chat.rainbow.toggle', false)
			export(@chatname.'.player.'.@player['id'].'.chat.rainbow.toggle', false)
			die(@prefix.color('d'). 'Automatic rainbow chat is now disabled!')
		} else {
		#enable
			store_value(@chatname.'.player.'.@player['id'].'.chat.rainbow.toggle', true)
			export(@chatname.'.player.'.@player['id'].'.chat.rainbow.toggle', true)
			die(@prefix.color('d')._rainbow('Automatic rainbow chat is now enabled!', import(@chatname.'.player.'.@player['id'].'.chat.rainbow.type', 'normal')).color('a').' Your chat will be like the colors of the rainbow now!')
		}
	}
#	default:
#		die(color('c').'['.color(7).@name.color('c').'] '.color('c').'Invalid setting specified. '.color('r').'Leave blank for normal rainbow chat, or type \'-doubles\' to use doubles mode.')
}	

#get saved chat type
@chattype = import(@chatname.'.player.'.@player['id'].'.chat.rainbow.type', 'normal')
#override with type specified
if(reg_count('^[-\\/][adwnp][los].*[ls2]$', to_lower(parse_args(@msg)[0])) > 0) {
	@msg = parse_args(@msg)
	@chattype = to_lower(reg_replace('[-\\/]', '', @msg[0]))
	array_remove(@msg, 0)
	@msg = array_implode(@msg, ' ')
}
#chat:
if(player() == '~console') {
	broadcast(_rainbow(@msg, @chattype))
} else {
	export(@chatname.'.player.'.@player['id'].'.chat.rainbow.cmd', true)
	chat(_rainbow(@msg, @chattype, 'chat'))
}
<<<

*:/rsay [$] = call_alias('/rainbow' $)