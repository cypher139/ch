#version 1.0-tmc
admin:/importmee6lb [$serverid] [$] = >>>
@name = 'Discord'
@id = $serverid
@args = parse_args($)
@config = import('DiscordBot.config.Discord')
if(array_contains(@args, 'help')) {
	msg('This imports the Mee6 Leaderboard data to CH storage, for use with a CH based leaderboard ranking system.')
	msg('specify the Mee6 server ID to use after the command. ex. if your leaderboard link is "https://mee6.xyz/en/leaderboard/123456" the server ID is "123456".')
	die()
}

proc _convert_json_data(@raw) {
	@json = json_decode(@raw)
	@config = import('DiscordBot.config.Discord')
	@newLeaderboard = array('LB_Guild_Name': @config['LB_Guild_Name'], 'lastedit': time(), 'members': array(), 'role_rewards': '', 'guild': @json['guild'], 'LB_XP_Per_Message': @json['xp_per_message'], 'LB_XP_Gain_Rate': @json['xp_rate'])
	foreach(@i: @user in @json['players']) {
		@userid = @json['players'][@i]['id']
		array_set(@newLeaderboard['members'], @userid, @user)
		array_remove(@newLeaderboard['members'][@userid], 'guild_id')
		array_remove(@newLeaderboard['members'][@userid], 'id')
		array_remove(@newLeaderboard['members'][@userid], 'is_monetize_subscriber')
		array_remove(@newLeaderboard['members'][@userid], 'monetize_xp_boost')
		array_set(@newLeaderboard['members'][@userid], 'tag', @json['players'][@i]['discriminator'])
		array_set(@newLeaderboard['members'][@userid], 'msg_count', @json['players'][@i]['message_count'])
		array_remove(@newLeaderboard['members'][@userid], 'discriminator')
		array_remove(@newLeaderboard['members'][@userid], 'message_count')
		array_set(@newLeaderboard['members'][@userid], 'last_msg', 0)
	}
	@rr = array('LB_XP_Per_Message': @json['xp_per_message'], 'LB_XP_Gain_Rate': @json['xp_rate'], 'LB_Role_Rewards': array())
	foreach(@j: @role in @json['role_rewards']) {
		array_set(@rr['LB_Role_Rewards'], integer(@role['rank']), array('RoleID': integer(@role['role']['id']), 'RoleName': @role['role']['name']))
	}
	@newLeaderboard['role_rewards'] = @rr
	#avatar:	https://cdn.discordapp.com/avatars/<USERID>/<AVATARID under {avatar: 123}>.webp?size=512
	msg('Leaderboard size: '.array_size(@newLeaderboard['members'])) 
	export('DiscordBot.Leaderboard', @newLeaderboard);
	store_value('DiscordBot.Leaderboard', @newLeaderboard);
	_write_file(import('DiscordBot.logs.dir').'/Converted-Mee6-Leaderboard-'.@json['guild']['name'].'.json', json_encode(@newLeaderboard), 'OVERWRITE')
	_write_file(import('DiscordBot.logs.dir').'/Converted-Mee6-RoleRewards-Config-'.@json['guild']['name'].'.yml', yml_encode(@rr, true), 'OVERWRITE')
	msg('The import of '.@json['guild']['name'].'\'s Leaderboard was successful.')
	msg('You will need to add the Role Rewards configuration manually to the DiscordBot Config file. A config file was provided to copypaste from.')
	return(true)
}


if($serverid == 'local') { 
	try { 
		@LeaderboardData = import('DiscordBot.logs.dir').'/Import-Leaderboard.json'
	} catch(Exception @ex) {
		die(@ex['message'])
	}
	_write_file(import('DiscordBot.logs.dir').'/Mee6-Leaderboard-'.@LeaderboardData['guild']['name'].'.json', @LeaderboardData, 'OVERWRITE')
	_convert_json_data(@LeaderboardData)
	die()
}
	
if(!is_numeric($serverid)) {
	die('Invalid Server ID given.')
}

if(array_contains(@args, 'view')) {
msg(get_value('DiscordBot.Leaderboard'))
die()
}

http_request('https://mee6.xyz/api/plugins/levels/leaderboard/'.$serverid.'?limit=999&page=0', array('blocking': true, 'success': closure(@data) {
	if(@data['error'] == true) {
		if(@data['responseCode'] == 404) {
			die('Guild "'.$serverid.'" was not found.')
		}
		msg('No relevant data received from server, did you typo the server ID?')
		export('DiscordBot.debug.http.error', @data)
		return(false)
	} else {
		_write_file(import('DiscordBot.logs.dir').'/Mee6-Leaderboard-'.json_decode(@data['body'])['guild']['name'].'.json', @data['body'], 'OVERWRITE')
		_convert_json_data(@data['body'])
		return(true)
	}
}));
<<<