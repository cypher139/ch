######################################################
#
#   random: Interesting functions to make your life as admin more fun!
#
######################################################

chaos:/sheepair [$action] [$] = >>>
#what is the name of this script?
@name = 'SheepAir'
@player = _getplayerID(player())

proc _sa_help() {
	msg(color(6).'SheepAir: spawn sheep by left clicking whereever you go!')
	msg(color(6).'Actions:')
	msg(color(7).'/sheepair '.color(4).' - '.color('f').'Enables/Disables: spawning sheep via left-clicking air.')
}

switch(to_lower($action)){

case 'help':
case '-help':
case '?':
case '/?':
	msg(color(7).'['.color('gold').@name.color('gold').'] '.color(7).rand(1,100))
	_sa_help()
	die()

default:
	if(player() == '~console') { die(color(7).'['.color('gold').@name.color(7).'] '.color(7).@name.' is not available for the console.')}
	@sheepenabled = import('sheep.spawner.enabled.'.@player['id'])
	if(@sheepenabled == false || @sheepenabled == '' || @sheepenabled == null) {
		export('sheep.spawner.enabled.'.@player['id'], true)
	#then check if bind is added
		if(!has_bind('sheep.spawner')) {
			bind('player_interact', array('id': 'sheep.spawner'), null, @sheepevent) {
				@player = _getplayerID(@sheepevent['player'])
				if(import('sheep.spawner.enabled.'.@player['id']) == true) {
					if(@sheepevent['action'] == 'left_click_air') {
						cancel()
						#colors from MC default textures
						@color = array('black': '#090a0f','red': '#b8342c','green': '#658619','brown': '#7e502f','blue': '#373a9f','purple': '#6f24a4','cyan': '#169b9c','light_gray': '#92928b','gray': '#43494c','pink': '#ea7c9f','lime': '#86cc26','yellow': '#fdd233','light_blue': '#41bbdf','magenta': '#d25bcb','orange': '#f67b18','white': '#fefefe')
						@type = array_get_rand(array_keys(@color))
						@here = pcursor(@sheepevent['player']) 
						@here[1] = 3 + @here[1]
						spawn_entity('sheep', 1, @here, closure(@id){ set_entity_spec(@id, array('color': @type)); })
						
						msg(color(7).'['.color(6).'Sheep'.color(7).'] '.color(@color[@type]).'Spawned a '.to_lower(@type).' sheep!'.color(8).' @ '.color(8).@here[0].','.@here[1].','.@here[2].color(8).')')
					}
				}
			}
		}
		die(color(7).'['.color('gold').@name.color(7).'] '.color(6).'Enabled!'.color(7).'Sheep enabled: Left-click to spawn sheep where you point. Use "/sheep" to disable.')
	} else {
		export('sheep.spawner.enabled.'.@player['id'], false)
		die(color(7).'['.color('gold').@name.color(7).'] '.color(7).'Sheep for '.@player['name'].color('a').' disabled.')
	}
}
<<<